plugins {
    id "java-library"
    id "io.qameta.allure" version "2.11.2"
    id "io.freefair.lombok" version "6.5.1"
}

group "io.swagger.petstore"
version "1.0-SNAPSHOT"


repositories {
    mavenCentral()
}

def allureVersion = "2.23.0",
    assertJVersion = "3.24.2",
    restAssuredVersion = "5.3.0"

dependencies {
    testImplementation(
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "org.junit.jupiter:junit-jupiter:5.9.1",
            "com.github.javafaker:javafaker:1.0.2",
            "org.assertj:assertj-core:$assertJVersion",
            "com.fasterxml.jackson.core:jackson-databind:2.19.0"
    )

    allure {
        version.set(allureVersion)
        adapter {
            allureJavaVersion.set(allureVersion)
            aspectjVersion.set("1.9.5")
            aspectjWeaver.set(true)

            frameworks {
                junit5 {
                    // Defaults to allureJavaVersion
                    adapterVersion.set(allureVersion)
                    enabled.set(true)
                }
            }
        }
    }

    testRuntimeOnly(
            "org.slf4j:slf4j-simple:2.0.5",
            "org.junit.jupiter:junit-jupiter-engine:5.9.1"
    )
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task smoke(type: Test) {
    useJUnitPlatform {
        includeTags("smoke")
    }
    systemProperties(System.getProperties())
}

task regress(type: Test) {
    useJUnitPlatform {
        includeTags("regress")
    }
    systemProperties(System.getProperties())
}
test {
    useJUnitPlatform()
    systemProperties(System.getProperties())
}